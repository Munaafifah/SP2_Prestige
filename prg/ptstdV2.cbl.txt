IDENTIFICATION DIVISION.
        PROGRAM-ID.    PTSTD.

      * PRINT STUDENT FILE - SELECTOR SCREEN
      * AUTHOR   DATE      TYPE   ACC    NOTES
      * MUNA   03/09/25     -     PA      SP1
      * MUNA   15/10/25     -     PA      SP2

       ENVIRONMENT DIVISION.
        INPUT-OUTPUT SECTION.
         FILE-CONTROL.
           COPY '/z/y25b31/sp2/lib/fd/fcstd'.
           COPY '/z/y25b31/sp2/lib/fd/fccy'.
           COPY '/z/y25b31/sp2/lib/fd/fcrc'.
           COPY '/z/y25b31/sp2/lib/fd/fcrg'.

       DATA DIVISION.
        FILE SECTION.
           COPY '/z/y25b31/sp2/lib/fd/fdstd'.
           COPY '/z/y25b31/sp2/lib/fd/fdcy'.
           COPY '/z/y25b31/sp2/lib/fd/fdrc'.
           COPY '/z/y25b31/sp2/lib/fd/fdrg'.

        WORKING-STORAGE SECTION.
           COPY '/z/y25b31/sp2/lib/fd/dbstd'.
           COPY '/z/y25b31/sp2/lib/fd/dbcy'.
           COPY '/z/y25b31/sp2/lib/fd/dbrc'.
           COPY '/z/y25b31/sp2/lib/fd/dbrg'.
           COPY '/v/cps/lib/std/stdvar.def'.
           COPY '/v/cps/lib/std/fkey.def'.
           COPY RESOURCE '/v/cps/lib/icon/help.jpg'.

        01 WS-MISC.
           03 WS-S-STD-KEY          PIC X(08).
           03 WS-E-STD-KEY          PIC X(08).
           03 WS-S-AGE              PIC 9(02).
           03 WS-E-AGE              PIC 9(02).
           03 WS-S-CY-KEY           PIC X(04).
           03 WS-E-CY-KEY           PIC X(04).
           03 WS-S-RC-KEY           PIC X(04).
           03 WS-E-RC-KEY           PIC X(04).
           03 WS-S-RG-KEY           PIC X(04).
           03 WS-E-RG-KEY           PIC X(04).
           03 WS-REPORT-TYPE        PIC 9(01).
           03 WS-SORT-MODE          PIC 9(01).
           03 WS-GENDER             PIC 9(02).
           03 REDEFINES WS-GENDER.
              05 WS-GENDERS         PIC 9(01) OCCURS 2.

        01 LINK-MISC.
           03 LINK-S-STD-KEY           PIC X(08).
           03 LINK-E-STD-KEY           PIC X(08).
           03 LINK-GENDER              PIC 9(02).
           03 LINK-GENDER2 REDEFINES LINK-GENDER
                                       PIC 9(01) OCCURS 2.
           03 LINK-S-AGE               PIC 9(02).
           03 LINK-E-AGE               PIC 9(02).
           03 LINK-S-CY-KEY            PIC X(04).
           03 LINK-E-CY-KEY            PIC X(04).
           03 LINK-S-RC-KEY            PIC X(04).
           03 LINK-E-RC-KEY            PIC X(04).
           03 LINK-S-RG-KEY            PIC X(04).
           03 LINK-E-RG-KEY            PIC X(04).

        LINKAGE SECTION.
        01 LINK-PROG-KEY            PIC X(30).

        SCREEN SECTION.
        01 SELECT-SCR.
           03 LABEL LINE 02 COL 04 'Student AC#:'.
           03 ENTRY-FIELD 3-D ID 101 COL 18 PIC X(08)
              SIZE 9 USING WS-S-STD-KEY AUTO.
           03 PUSH-BUTTON 'F10 - Help Table' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 101.
           03 LABEL COL + 3 'to'.
           03 ENTRY-FIELD 3-D ID 102 COL + 3 PIC X(08)
              SIZE 9 USING WS-E-STD-KEY.
           03 PUSH-BUTTON 'F10 - Help Table' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 102.

           03 LABEL LINE + 1 COL 04 'Gender:'.
           03 CHECK-BOX ID 201 LINE + 0.2 COL 18 PIC 9(01)
              USING WS-GENDERS(1).
           03 LABEL LINE - 0.2 COL + 1.5 'Male'.
           03 CHECK-BOX ID 202 LINE + 0.2 COL + 3 PIC 9(01)
              USING WS-GENDERS(2).
           03 LABEL LINE - 0.2 COL + 1.5 'Female'.

           03 LABEL LINE + 1 COL 04 'Age:'.
           03 ENTRY-FIELD 3-D ID 301 COL 18 PIC 99
              SIZE 3 USING WS-S-AGE.
           03 LABEL COL + 3 'to'.
           03 ENTRY-FIELD 3-D ID 302 COL + 3 PIC 99
              SIZE 3 USING WS-E-AGE.

           03 LABEL LINE + 1 COL 04 'Country:'.
           03 ENTRY-FIELD 3-D ID 401 COL 18 PIC X(04)
              SIZE 5 USING WS-S-CY-KEY AUTO.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 401.
           03 LABEL COL + 3 'to'.
           03 ENTRY-FIELD 3-D ID 402 COL + 3 PIC X(04)
              SIZE 5 USING WS-E-CY-KEY.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 402.

           03 LABEL LINE + 1 COL 04 'Race:'.
           03 ENTRY-FIELD 3-D ID 501 COL 18 PIC X(04)
              SIZE 5 USING WS-S-RC-KEY AUTO.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 501.
           03 LABEL COL + 3 'to'.
           03 ENTRY-FIELD 3-D ID 502 COL + 3 PIC X(04)
              SIZE 5 USING WS-E-RC-KEY.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 502.

           03 LABEL LINE + 1 COL 04 'Religion:'.
           03 ENTRY-FIELD 3-D ID 601 COL 18 PIC X(04)
              SIZE 5 USING WS-S-RG-KEY AUTO.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5 LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 601.
           03 LABEL COL + 3 'to'.
           03 ENTRY-FIELD 3-D ID 602 COL + 3 PIC X(04)
              SIZE 5 USING WS-E-RG-KEY.
           03 PUSH-BUTTON 'F10 - Help' NO-TAB
              COL + 1.5  LINES 13 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER      = 1
              TERMINATION-VALUE  = 602.

           03 LABEL LINE + 2 COL 04 'Select Report Type'.
           03 LABEL LINE + 1 COL 04 'Report Type:'.
           03 RADIO-BUTTON LINE + 0.2 COL 18 GROUP = 2 GROUP-VALUE = 1
              VALUE WS-REPORT-TYPE.
           03 LABEL LINE - 0.2 COL + 1.5 'Simple'.
           03 RADIO-BUTTON LINE + 0.2 COL + 3 GROUP = 2
              GROUP-VALUE = 2 VALUE WS-REPORT-TYPE.
           03 LABEL LINE - 0.2 COL + 1.5 'Detail'.

           03 LABEL LINE + 1 COL 04 'Sort Mode:'.
           03 RADIO-BUTTON LINE + 0.2 COL 18 GROUP = 3 GROUP-VALUE = 1
              VALUE WS-SORT-MODE.
           03 LABEL LINE - 0.2 COL + 1.5 'By AC#'.
           03 RADIO-BUTTON LINE + 1 COL 18 GROUP = 3 GROUP-VALUE = 2
              VALUE WS-SORT-MODE.
           03 LABEL LINE - 0.2 COL + 1.5 'By Name'.
           03 RADIO-BUTTON LINE + 1 COL 18 GROUP = 3 GROUP-VALUE = 3
              VALUE WS-SORT-MODE.
           03 LABEL LINE - 0.2 COL + 1.5 'By Age + Name'.
           03 RADIO-BUTTON LINE + 1 COL 18 GROUP = 3 GROUP-VALUE = 4
              VALUE WS-SORT-MODE.
           03 LABEL LINE - 0.2 COL + 1.5 'By Country + Name'.

           03 PUSH-BUTTON 'OK' SELF-ACT
              LINE 15 COL 20 LINES 1.2 SIZE 12
              TERMINATION-VALUE = 013.
           03 PUSH-BUTTON 'Cancel' SELF-ACT
              COL + 2 LINES 1.2 SIZE 12
              TERMINATION-VALUE = 027.

      ******************************************************************
       PROCEDURE DIVISION USING LINK-PROG-KEY.

        DECLARATIVES.

           COPY '/z/y25b31/sp2/lib/fd/dcstd'.
           COPY '/z/y25b31/sp2/lib/fd/dccy'.
           COPY '/z/y25b31/sp2/lib/fd/dcrc'.
           COPY '/z/y25b31/sp2/lib/fd/dcrg'.

        END DECLARATIVES.

      ******************************************************************
        BEGIN.

           MOVE 'N' TO S-RUN.
           OPEN INPUT STD-FILE.
           OPEN INPUT CY-FILE.
           OPEN INPUT RC-FILE.
           OPEN INPUT RG-FILE.

           COPY '/v/cps/lib/std/gtcoid.prd'.
           MOVE 'Print Student Profile' TO S-WINDOW-TITLE.

           DISPLAY FLOATING WINDOW LINES 16 SIZE 65 COLOR 65793
           CELL SIZE =ENTRY-FIELD FONT SEPARATE
           TITLE-BAR MODAL NO SCROLL NO WRAP
           TITLE S-WINDOW-TITLE
           POP-UP S-WINDOW.

           CALL 'W$BITMAP' USING
                WBITMAP-LOAD, 'help.jpg' GIVING S-BITMAP.

           INITIALIZE WS-MISC.
           MOVE 1 TO WS-REPORT-TYPE.
           MOVE 1 TO WS-SORT-MODE.
           MOVE 'Y' TO S-RUN.
           PERFORM 0100-MAIN THRU 0199-END UNTIL S-RUN = 'N'.

        TERMINATION.
           CLOSE WINDOW S-WINDOW.
           CLOSE STD-FILE.
           CLOSE CY-FILE.
           CLOSE RC-FILE.
           CLOSE RG-FILE.
           EXIT PROGRAM.
           STOP RUN.

      ******************************************************************
        0100-MAIN.

           PERFORM ERROR-RTN THRU ERROR-END.
           DISPLAY SELECT-SCR.
           ACCEPT  SELECT-SCR.
           MOVE 4 TO ACCEPT-CONTROL.

           IF K-ESCAPE
               MOVE 'N' TO S-RUN GO TO 0199-END.

      * Help for Student ID Start
           IF (K-F10 AND S-CONTROL-ID = 101) OR KEY-STATUS = 101
              CALL   '/z/y25b31/sp2/prg/hpstd' USING WS-S-STD-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpstd'
              MOVE 101 TO S-CONTROL-ID
              IF S-OK = 'Y'
                  MOVE 102 TO S-CONTROL-ID
              END-IF
              GO TO 0100-MAIN.

      * Help for Student ID End
           IF (K-F10 AND S-CONTROL-ID = 102) OR KEY-STATUS = 102
              CALL   '/z/y25b31/sp2/prg/hpstd' USING WS-E-STD-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpstd'
              MOVE 102 TO S-CONTROL-ID
              GO TO 0100-MAIN.

      * Help for Country Start
           IF (K-F10 AND S-CONTROL-ID = 401) OR KEY-STATUS = 401
              CALL   '/z/y25b31/sp2/prg/hpcy' USING WS-S-CY-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpcy'
              MOVE 401 TO S-CONTROL-ID
              IF S-OK = 'Y'
                  MOVE 402 TO S-CONTROL-ID
              END-IF
              GO TO 0100-MAIN.

      * Help for Country End
           IF (K-F10 AND S-CONTROL-ID = 402) OR KEY-STATUS = 402
              CALL   '/z/y25b31/sp2/prg/hpcy' USING WS-E-CY-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpcy'
              MOVE 402 TO S-CONTROL-ID
              GO TO 0100-MAIN.

      * Help for Race Start
           IF (K-F10 AND S-CONTROL-ID = 501) OR KEY-STATUS = 501
              CALL   '/z/y25b31/sp2/prg/hprc' USING WS-S-RC-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprc'
              MOVE 501 TO S-CONTROL-ID
              IF S-OK = 'Y'
                  MOVE 502 TO S-CONTROL-ID
              END-IF
              GO TO 0100-MAIN.

      * Help for Race End
           IF (K-F10 AND S-CONTROL-ID = 502) OR KEY-STATUS = 502
              CALL   '/z/y25b31/sp2/prg/hprc' USING WS-E-RC-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprc'
              MOVE 502 TO S-CONTROL-ID
              GO TO 0100-MAIN.

      * Help for Religion Start
           IF (K-F10 AND S-CONTROL-ID = 601) OR KEY-STATUS = 601
              CALL   '/z/y25b31/sp2/prg/hprg' USING WS-S-RG-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprg'
              MOVE 601 TO S-CONTROL-ID
              IF S-OK = 'Y'
                  MOVE 602 TO S-CONTROL-ID
              END-IF
              GO TO 0100-MAIN.

      * Help for Religion End
           IF (K-F10 AND S-CONTROL-ID = 602) OR KEY-STATUS = 602
              CALL   '/z/y25b31/sp2/prg/hprg' USING WS-E-RG-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprg'
              MOVE 602 TO S-CONTROL-ID
              GO TO 0100-MAIN.

           IF NOT K-ENTER GO TO 0100-MAIN.

      * Validate ranges
           IF WS-E-STD-KEY NOT = SPACES AND
              WS-S-STD-KEY > WS-E-STD-KEY
               MOVE 100035 TO S-ERROR-CODE
               MOVE 101    TO S-CONTROL-ID
               GO TO 0100-MAIN.

           IF WS-E-AGE NOT = ZEROS AND
              WS-S-AGE > WS-E-AGE
               MOVE 100035 TO S-ERROR-CODE
               MOVE 301    TO S-CONTROL-ID
               GO TO 0100-MAIN.

           IF WS-E-CY-KEY NOT = SPACES AND
              WS-S-CY-KEY > WS-E-CY-KEY
               MOVE 100035 TO S-ERROR-CODE
               MOVE 401    TO S-CONTROL-ID
               GO TO 0100-MAIN.

           IF WS-E-RC-KEY NOT = SPACES AND
              WS-S-RC-KEY > WS-E-RC-KEY
               MOVE 100035 TO S-ERROR-CODE
               MOVE 501    TO S-CONTROL-ID
               GO TO 0100-MAIN.

           IF WS-E-RG-KEY NOT = SPACES AND
              WS-S-RG-KEY > WS-E-RG-KEY
               MOVE 100035 TO S-ERROR-CODE
               MOVE 601    TO S-CONTROL-ID
               GO TO 0100-MAIN.

           IF WS-GENDER = ZEROES
             MOVE 100020 TO S-ERROR-CODE
             MOVE 201    TO S-CONTROL-ID
             GO TO 0100-MAIN.

      * Call PSSTD to process and create temporary file
           MOVE 101 TO S-CONTROL-ID.
           MOVE WS-S-STD-KEY  TO LINK-S-STD-KEY.
           MOVE WS-E-STD-KEY  TO LINK-E-STD-KEY.
           MOVE WS-GENDER     TO LINK-GENDER.
           MOVE WS-S-AGE      TO LINK-S-AGE.
           MOVE WS-E-AGE      TO LINK-E-AGE.
           MOVE WS-S-CY-KEY   TO LINK-S-CY-KEY.
           MOVE WS-E-CY-KEY   TO LINK-E-CY-KEY.
           MOVE WS-S-RC-KEY   TO LINK-S-RC-KEY.
           MOVE WS-E-RC-KEY   TO LINK-E-RC-KEY.
           MOVE WS-S-RG-KEY   TO LINK-S-RG-KEY.
           MOVE WS-E-RG-KEY   TO LINK-E-RG-KEY.

      * Handle blank values - Set to LOW/HIGH for LINKAGE only
           IF LINK-S-STD-KEY = SPACES
              MOVE LOW-VALUE TO LINK-S-STD-KEY.
           IF LINK-E-STD-KEY = SPACES
              MOVE HIGH-VALUE TO LINK-E-STD-KEY.

           IF LINK-S-AGE = ZEROS
              MOVE 00 TO LINK-S-AGE.
           IF LINK-E-AGE = ZEROS
              MOVE 99 TO LINK-E-AGE.

           IF LINK-S-CY-KEY = SPACES
              MOVE LOW-VALUE TO LINK-S-CY-KEY.
           IF LINK-E-CY-KEY = SPACES
              MOVE HIGH-VALUE TO LINK-E-CY-KEY.

           IF LINK-S-RC-KEY = SPACES
              MOVE LOW-VALUE TO LINK-S-RC-KEY.
           IF LINK-E-RC-KEY = SPACES
              MOVE HIGH-VALUE TO LINK-E-RC-KEY.

           IF LINK-S-RG-KEY = SPACES
              MOVE LOW-VALUE TO LINK-S-RG-KEY.
           IF LINK-E-RG-KEY = SPACES
              MOVE HIGH-VALUE TO LINK-E-RG-KEY.


           CALL   '/z/y25b31/sp2/prg/psstd' USING
                  WS-S-STD-KEY,
                  LINK-MISC,
                  S-OK
           CANCEL '/z/y25b31/sp2/prg/psstd'.

           IF S-OK = 'Y'
              IF WS-REPORT-TYPE = 1
                 CALL   '/z/y25b31/sp2/prg/ptstd1' USING
                        LINK-PROG-KEY,
                        WS-S-STD-KEY,
                        WS-SORT-MODE
                 CANCEL '/z/y25b31/sp2/prg/ptstd1'
              ELSE
                 CALL   '/z/y25b31/sp2/prg/ptstd2' USING
                        LINK-PROG-KEY,
                        WS-S-STD-KEY,
                        WS-SORT-MODE
                 CANCEL '/z/y25b31/sp2/prg/ptstd2'
              END-IF
           END-IF.

        0199-END. EXIT.

      ******************************************************************
           COPY '/v/cps/lib/std/errmsg.prd'.

      * End of program.
