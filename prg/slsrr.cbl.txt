IDENTIFICATION DIVISION.
        PROGRAM-ID. SLSRR.

      * REPORT MANAGEMENT -RPMENU > SLSRR
      * AUTHOR  DATE       TYPE  A/C  NOTES
      * MUNA    15/10/25   -     PA   SP2

       ENVIRONMENT DIVISION.
       DATA DIVISION.
        WORKING-STORAGE SECTION.
           COPY '/v/cps/lib/std/stdvar.def'.
           COPY '/v/cps/lib/std/fkey.def'.
           COPY RESOURCE '/v/cps/lib/icon/help.jpg'.

        01 WS-OPTION.
           03 WS-S-CY-KEY     PIC X(04).
           03 WS-E-CY-KEY     PIC X(04).
           03 WS-S-RC-KEY     PIC X(04).
           03 WS-E-RC-KEY     PIC X(04).
           03 WS-S-RG-KEY     PIC X(04).
           03 WS-E-RG-KEY     PIC X(04).
           03 WS-GENDER       PIC 9(02).
           03 REDEFINES WS-GENDER.
              05 WS-GENDERS   PIC 9(01) OCCURS 2.

        LINKAGE SECTION.
        01 LINK-PROG-KEY      PIC X(30).

        SCREEN SECTION.
        01 SELECT-SCR.
           03 LABEL LINE 02 COL 04 'Country:'.
           03 ENTRY-FIELD 3-D ID 101 COL + 08 PIC X(04)
              USING WS-S-CY-KEY AUTO.
           03 PUSH-BUTTON 'Country Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 101.
           03 LABEL COL + 1.5 'to'.
           03 ENTRY-FIELD 3-D ID 102 COL + 1.5 PIC X(04)
              USING WS-E-CY-KEY.
           03 PUSH-BUTTON 'Country Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 102.

           03 LABEL LINE 03 COL 04 'Race:'.
           03 ENTRY-FIELD 3-D ID 103 COL + 10 PIC X(04)
              USING WS-S-RC-KEY AUTO.
           03 PUSH-BUTTON 'Race Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 103.
           03 LABEL COL + 1.5 'to'.
           03 ENTRY-FIELD 3-D ID 104 COL + 1.5 PIC X(04)
              USING WS-E-RC-KEY.
           03 PUSH-BUTTON 'Race Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 104.

           03 LABEL LINE 04 COL 04 'Religion:'.
           03 ENTRY-FIELD 3-D ID 105 COL + 7.7 PIC X(04)
              USING WS-S-RG-KEY AUTO.
           03 PUSH-BUTTON 'Religion Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 105.
           03 LABEL COL + 1.5 'to'.
           03 ENTRY-FIELD 3-D ID 106 COL + 1.5 PIC X(04)
              USING WS-E-RG-KEY.
           03 PUSH-BUTTON 'Religion Table' BITMAP NO-TAB FLAT
              LINE - 0.05 COL + 1.5 LINES 19 SIZE 19 BITMAP
              BITMAP-HANDLE S-BITMAP
              BITMAP-NUMBER        = 1
              TERMINATION-VALUE    = 106.

           03 LABEL LINE 05 COL 04 'Gender:'.
           03 CHECK-BOX ID 107 LINE + 0.2 COL + 8 PIC 9(01)
              USING WS-GENDERS(1).
           03 LABEL LINE - 0.2 COL + 1.5 'Male'.
           03 CHECK-BOX ID 108 LINE + 0.2 COL + 6 PIC 9(01)
              USING WS-GENDERS(2).
           03 LABEL LINE - 0.2 COL + 1.5 'Female'.

           COPY '/v/cps/lib/std/ptbtn.scr'.

      ***************************************************************
       PROCEDURE DIVISION USING LINK-PROG-KEY.
       BEGIN.

           CALL   '/z/y25b31/sp2/lib/std/f-gttid' USING S-DATA-ID.
           CANCEL '/z/y25b31/sp2/lib/std/f-gttid'.

           MOVE 'N' TO S-RUN.
           MOVE 'Print Student Statistic Report' TO S-WINDOW-TITLE.
           COPY '/v/cps/lib/std/ptwin.prd'.

           CALL 'W$BITMAP' USING
                WBITMAP-LOAD, 'help.jpg' GIVING S-BITMAP.

           INITIALIZE WS-OPTION.
           MOVE 11  TO WS-GENDER.
           MOVE 'Y' TO S-RUN.
           PERFORM 0100-MAIN THRU 0199-END UNTIL S-RUN = 'N'.

        TERMINATION.
           CLOSE WINDOW S-WINDOW.
           EXIT PROGRAM.
           STOP RUN.

      ***************************************************************
       0100-MAIN.
           MOVE 101 TO S-CONTROL-ID.

       0110-MAIN.
           PERFORM ERROR-RTN THRU ERROR-END.
           DISPLAY SELECT-SCR.
           ACCEPT  SELECT-SCR.
           MOVE 4 TO ACCEPT-CONTROL.

           IF K-ESCAPE
              MOVE 'N' TO S-RUN GO TO 0199-END.

           IF (K-F10 AND S-CONTROL-ID = 101) OR KEY-STATUS = 101
              CALL   '/z/y25b31/sp2/prg/hpcy' USING WS-S-CY-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpcy'
              MOVE 101 TO S-CONTROL-ID
              IF S-OK = 'Y'
                 MOVE 102 TO S-CONTROL-ID
              END-IF
              GO TO 0110-MAIN.

           IF (K-F10 AND S-CONTROL-ID = 102) OR KEY-STATUS = 102
              CALL   '/z/y25b31/sp2/prg/hpcy' USING WS-E-CY-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hpcy'
              MOVE 102 TO S-CONTROL-ID
              IF S-OK ='Y'
                 MOVE 103 TO S-CONTROL-ID
              END-IF
              GO TO 0110-MAIN.

           IF (K-F10 AND S-CONTROL-ID = 103) OR KEY-STATUS = 103
              CALL   '/z/y25b31/sp2/prg/hprc' USING WS-S-RC-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprc'
              MOVE 103 TO S-CONTROL-ID
              IF S-OK = 'Y'
                 MOVE 104 TO S-CONTROL-ID
              END-IF
              GO TO 0110-MAIN.

           IF (K-F10 AND S-CONTROL-ID = 104) OR KEY-STATUS = 104
              CALL   '/z/y25b31/sp2/prg/hprc' USING WS-E-RC-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprc'
              MOVE 104 TO S-CONTROL-ID
              IF S-OK = 'Y'
                 MOVE 105 TO S-CONTROL-ID
              END-IF
              GO TO 0110-MAIN.

           IF (K-F10 AND S-CONTROL-ID = 105) OR KEY-STATUS = 105
              CALL   '/z/y25b31/sp2/prg/hprg' USING
                     WS-S-RG-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprg'
              MOVE 105 TO S-CONTROL-ID
              IF S-OK = 'Y'
                 MOVE 106 TO S-CONTROL-ID
              END-IF
              GO TO 0110-MAIN.

           IF (K-F10 AND S-CONTROL-ID = 106) OR KEY-STATUS = 106
              CALL   '/z/y25b31/sp2/prg/hprg' USING
                     WS-E-RG-KEY, S-OK
              CANCEL '/z/y25b31/sp2/prg/hprg'
              MOVE 106 TO S-CONTROL-ID
              GO TO 0110-MAIN.

           IF NOT K-ENTER GO TO 0110-MAIN.

           IF WS-GENDER = ZEROES
              MOVE 100020 TO S-ERROR-CODE
              MOVE 107    TO S-CONTROL-ID
              GO TO 0110-MAIN.

           IF WS-E-CY-KEY NOT = SPACES AND
              WS-S-CY-KEY > WS-E-CY-KEY
              MOVE 100035 TO S-ERROR-CODE
              MOVE 101    TO S-CONTROL-ID
              GO TO 0110-MAIN.

           IF WS-E-RC-KEY NOT = SPACES AND
              WS-S-RC-KEY > WS-E-RC-KEY
              MOVE 100035 TO S-ERROR-CODE
              MOVE 103    TO S-CONTROL-ID
              GO TO 0110-MAIN.

           IF WS-E-RG-KEY NOT = SPACES AND
              WS-S-RG-KEY > WS-E-RG-KEY
              MOVE 100035 TO S-ERROR-CODE
              MOVE 105    TO S-CONTROL-ID
              GO TO 0110-MAIN.

           IF WS-S-CY-KEY = SPACES
              MOVE LOW-VALUE  TO WS-S-CY-KEY.
           IF WS-E-CY-KEY = SPACES
              MOVE HIGH-VALUE TO WS-E-CY-KEY.

           IF WS-S-RC-KEY = SPACES
              MOVE LOW-VALUE  TO WS-S-RC-KEY.
           IF WS-E-RC-KEY = SPACES
              MOVE HIGH-VALUE TO WS-E-RC-KEY.

           IF WS-S-RG-KEY = SPACES
              MOVE LOW-VALUE  TO WS-S-RG-KEY.
           IF WS-E-RG-KEY = SPACES
              MOVE HIGH-VALUE TO WS-E-RG-KEY.

      *      CLOSE WINDOW S-WINDOW.

      * Call PSSRR to process data
           CALL   '/z/y25b31/sp2/prg/pssrr' USING
                  LINK-PROG-KEY, S-DATA-ID, WS-OPTION, S-OK.
           CANCEL '/z/y25b31/sp2/prg/pssrr'.

      * If processing successful, call PTSRR to print
           IF S-OK NOT = 'Y'
              GO TO 0190-MAIN.

           CALL   '/z/y25b31/sp2/prg/ptsrr' USING
                  LINK-PROG-KEY, S-DATA-ID.
           CANCEL '/z/y25b31/sp2/prg/ptsrr'.

      *OPEN WINDOW S-WINDOW.

       0190-MAIN.
           IF WS-S-CY-KEY = LOW-VALUE
              INITIALIZE WS-S-CY-KEY.

           IF WS-S-RC-KEY = LOW-VALUE
              INITIALIZE WS-S-RC-KEY.

           IF WS-S-RG-KEY = LOW-VALUE
              INITIALIZE WS-S-RG-KEY.

           IF WS-E-CY-KEY = HIGH-VALUE
              INITIALIZE WS-E-CY-KEY.

           IF WS-E-RC-KEY = HIGH-VALUE
              INITIALIZE WS-E-RC-KEY.

           IF WS-E-RG-KEY = HIGH-VALUE
              INITIALIZE WS-E-RG-KEY.

       0199-END.         EXIT.
      ***************************************************************
       COPY '/v/cps/lib/std/errmsg.prd'.
      * End of Program
